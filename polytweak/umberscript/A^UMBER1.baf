IF
	See(NearestEnemyOf(Myself))
	Global("Encounter","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Encounter","LOCALS",1)
		SetGlobalTimer("Combat","LOCALS",2000)
		SetGlobalTimer("AttackTactic","LOCALS",12)
		Help()
		Continue()
END

IF
	!GlobalTimerNotExpired("Combat","LOCALS")
	Global("Encounter","LOCALS",1)
	!Detect([GOODCUTOFF])
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("Encounter","LOCALS",0)
		SetGlobal("SpAbility","LOCALS",0)
		ApplySpellRES("SPIN711",Myself) // Heal
		Rest()
END

IF
	Allegiance(Myself,NEUTRAL) //They should spawn as enemies anyway, but stranger things have happened...
	OR(3)
		AttackedBy([GOODCUTOFF],DEFAULT)
		SpellCastOnMe([GOODCUTOFF],0)
		Help([EVILCUTOFF.0.UMBERHULK])
THEN
	RESPONSE #100
		Enemy()
		Continue()
END

IF
	See([GOODCUTOFF])
	Global("Search","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("Search","LOCALS",0)
		SetGlobalTimer("SearchT","LOCALS",0)
		Help()
END

IF
	Global("Encounter","LOCALS",1)
	!See(NearestEnemyOf(Myself))
	Global("Search","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Search","LOCALS",1)
		SetGlobalTimer("SearchT","LOCALS",30)
END

IF
	Global("Search","LOCALS",0)
	OR(2)
		AttackedBy([ANYONE],DEFAULT)
		TookDamage()
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("Search","LOCALS",1)
		SetGlobalTimer("SearchT","LOCALS",30)
END

IF
	Global("Search","LOCALS",1)
	GlobalTimerNotExpired("SearchT","LOCALS")
	InMyArea(Player6)
	HPGT(Player6,1)
	!StateCheck(Player6,STATE_INVISIBLE)
	!CheckStatGT(Player6,0,SANCTUARY)
THEN
	RESPONSE #15
		MoveToObject(Player6)
	RESPONSE #85
		Continue()
END

IF
	Global("Search","LOCALS",1)
	GlobalTimerNotExpired("SearchT","LOCALS")
	InMyArea(Player5)
	HPGT(Player5,1)
	!StateCheck(Player5,STATE_INVISIBLE)
	!CheckStatGT(Player5,0,SANCTUARY)
THEN
	RESPONSE #20
		MoveToObject(Player5)
	RESPONSE #80
		Continue()
END

IF
	Global("Search","LOCALS",1)
	GlobalTimerNotExpired("SearchT","LOCALS")
	InMyArea(Player4)
	HPGT(Player4,1)
	!StateCheck(Player4,STATE_INVISIBLE)
	!CheckStatGT(Player4,0,SANCTUARY)
THEN
	RESPONSE #25
		MoveToObject(Player4)
	RESPONSE #75
		Continue()
END

IF
	Global("Search","LOCALS",1)
	GlobalTimerNotExpired("SearchT","LOCALS")
	InMyArea(Player3)
	HPGT(Player3,1)
	!StateCheck(Player3,STATE_INVISIBLE)
	!CheckStatGT(Player3,0,SANCTUARY)
THEN
	RESPONSE #30
		MoveToObject(Player3)
	RESPONSE #70
		Continue()
END

IF
	Global("Search","LOCALS",1)
	GlobalTimerNotExpired("SearchT","LOCALS")
	InMyArea(Player2)
	HPGT(Player2,1)
	!StateCheck(Player2,STATE_INVISIBLE)
	!CheckStatGT(Player2,0,SANCTUARY)
THEN
	RESPONSE #50
		MoveToObject(Player2)
	RESPONSE #50
		Continue()
END

IF
	Global("Search","LOCALS",1)
	GlobalTimerNotExpired("SearchT","LOCALS")
	InMyArea(Player1)
	!StateCheck(Player1,STATE_INVISIBLE)
	!CheckStatGT(Player1,0,SANCTUARY)
THEN
	RESPONSE #100
		MoveToObject(Player1)
END

IF
	Help([EVILCUTOFF.0.UMBERHULK])
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		MoveToObject(LastHelp(Myself))
END

IF
	Global("Search","LOCALS",1)
	!See(NearestEnemyOf(Myself))
	GlobalTimerNotExpired("SearchT","LOCALS")
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	OR(4)
		!Allegiance(Myself,EVILCUTOFF)
		StateCheck(Myself,STATE_SLEEPING)
		StateCheck(Myself,STATE_STUNNED)
		CheckStatGT(Myself,0,HELD)
THEN
	RESPONSE #100
		NoAction()
END

//After two rounds of attacking the best target, there is a 35% chance per round to attack
//the nearest enemy or last attacker, so as not to get hung up on chasing unreachable PC's

IF
	Global("Encounter","LOCALS",1)
	!GlobalTimerNotExpired("AttackTactic","LOCALS")
	Global("Target","LOCALS",0)
THEN
	RESPONSE #35
		SetGlobal("Target","LOCALS",1)
		SetGlobalTimer("AttackTactic","LOCALS",6)
		Help()
	RESPONSE #65
		SetGlobalTimer("AttackTactic","LOCALS",6)
		Help()
END

IF
	Global("Encounter","LOCALS",1)
	!GlobalTimerNotExpired("AttackTactic","LOCALS")
	Global("Target","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("Target","LOCALS",0)
		SetGlobalTimer("AttackTactic","LOCALS",6)
		Help()
END

//Can't be bothered to bundle DS and the assorted fixes in with Polytweak, so I use a timer to
//keep track of protection from weapon type spells

IF
	SpellCast(Player1,WIZARD_MANTLE)
	!HasItemEquiped("A^UMBER2",Myself) // Attack
THEN
	RESPONSE #100
		SetGlobalTimer("AVP1","LOCALS",24)
		ClearActions(Myself)
END

IF
	SpellCast(Player2,WIZARD_MANTLE)
	!HasItemEquiped("A^UMBER2",Myself) // Attack
THEN
	RESPONSE #100
		SetGlobalTimer("AVP2","LOCALS",24)
		ClearActions(Myself)
END

IF
	SpellCast(Player3,WIZARD_MANTLE)
	!HasItemEquiped("A^UMBER2",Myself) // Attack
THEN
	RESPONSE #100
		SetGlobalTimer("AVP3","LOCALS",24)
		ClearActions(Myself)
END

IF
	SpellCast(Player4,WIZARD_MANTLE)
	!HasItemEquiped("A^UMBER2",Myself) // Attack
THEN
	RESPONSE #100
		SetGlobalTimer("AVP4","LOCALS",24)
		ClearActions(Myself)
END

IF
	SpellCast(Player5,WIZARD_MANTLE)
	!HasItemEquiped("A^UMBER2",Myself) // Attack
THEN
	RESPONSE #100
		SetGlobalTimer("AVP5","LOCALS",24)
		ClearActions(Myself)
END

IF
	SpellCast(Player6,WIZARD_MANTLE)
	!HasItemEquiped("A^UMBER2",Myself) // Attack
THEN
	RESPONSE #100
		SetGlobalTimer("AVP6","LOCALS",24)
		ClearActions(Myself)
END

IF
	OR(3)
		SpellCast(Player1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SpellCast(Player1,WIZARD_ABSOLUTE_IMMUNITY)
		SpellCast(Player1,WIZARD_IMPROVED_MANTLE)
THEN
	RESPONSE #100
		SetGlobalTimer("AVP1","LOCALS",24)
		ClearActions(Myself)
END

IF
	OR(3)
		SpellCast(Player2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SpellCast(Player2,WIZARD_ABSOLUTE_IMMUNITY)
		SpellCast(Player2,WIZARD_IMPROVED_MANTLE)
THEN
	RESPONSE #100
		SetGlobalTimer("AVP2","LOCALS",24)
		ClearActions(Myself)
END

IF
	OR(3)
		SpellCast(Player3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SpellCast(Player3,WIZARD_ABSOLUTE_IMMUNITY)
		SpellCast(Player3,WIZARD_IMPROVED_MANTLE)
THEN
	RESPONSE #100
		SetGlobalTimer("AVP3","LOCALS",24)
		ClearActions(Myself)
END

IF
	OR(3)
		SpellCast(Player4,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SpellCast(Player4,WIZARD_ABSOLUTE_IMMUNITY)
		SpellCast(Player4,WIZARD_IMPROVED_MANTLE)
THEN
	RESPONSE #100
		SetGlobalTimer("AVP4","LOCALS",24)
		ClearActions(Myself)
END

IF
	OR(3)
		SpellCast(Player5,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SpellCast(Player5,WIZARD_ABSOLUTE_IMMUNITY)
		SpellCast(Player5,WIZARD_IMPROVED_MANTLE)
THEN
	RESPONSE #100
		SetGlobalTimer("AVP5","LOCALS",24)
		ClearActions(Myself)
END

IF
	OR(3)
		SpellCast(Player6,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SpellCast(Player6,WIZARD_ABSOLUTE_IMMUNITY)
		SpellCast(Player6,WIZARD_IMPROVED_MANTLE)
THEN
	RESPONSE #100
		SetGlobalTimer("AVP6","LOCALS",24)
		ClearActions(Myself)
END

//Gaze isn't supposed to be intelligently targetted, just whoever happens to make eye contact
//Likelihood depends on proximity

IF
	!GlobalTimerNotExpired("gaze","LOCALS")
	Range(SixthNearestEnemyOf(Myself),12)
	See(SixthNearestEnemyOf(Myself))
THEN
	RESPONSE #10
		ForceSpellRES("A^UMGAZE",LastSeenBy(Myself)) // Disorienting Gaze
		SetSequence(SEQ_READY)
		SetGlobalTimer("gaze","LOCALS",6)
	RESPONSE #90
		Continue()
END

IF
	!GlobalTimerNotExpired("gaze","LOCALS")
	Range(FifthNearestEnemyOf(Myself),12)
	See(FifthNearestEnemyOf(Myself))
THEN
	RESPONSE #15
		ForceSpellRES("A^UMGAZE",LastSeenBy(Myself)) // Disorienting Gaze
		SetSequence(SEQ_READY)
		SetGlobalTimer("gaze","LOCALS",6)
	RESPONSE #85
		Continue()
END

IF
	!GlobalTimerNotExpired("gaze","LOCALS")
	Range(FourthNearestEnemyOf(Myself),12)
	See(FourthNearestEnemyOf(Myself))
THEN
	RESPONSE #20
		ForceSpellRES("A^UMGAZE",LastSeenBy(Myself)) // Disorienting Gaze
		SetSequence(SEQ_READY)
		SetGlobalTimer("gaze","LOCALS",6)
	RESPONSE #80
		Continue()
END

IF
	!GlobalTimerNotExpired("gaze","LOCALS")
	Range(ThirdNearestEnemyOf(Myself),12)
	See(ThirdNearestEnemyOf(Myself))
THEN
	RESPONSE #33
		ForceSpellRES("A^UMGAZE",LastSeenBy(Myself)) // Disorienting Gaze
		SetSequence(SEQ_READY)
		SetGlobalTimer("gaze","LOCALS",6)
	RESPONSE #67
		Continue()
END

IF
	!GlobalTimerNotExpired("gaze","LOCALS")
	Range(SecondNearestEnemyOf(Myself),12)
	See(SecondNearestEnemyOf(Myself))
THEN
	RESPONSE #50
		ForceSpellRES("A^UMGAZE",LastSeenBy(Myself)) // Disorienting Gaze
		SetSequence(SEQ_READY)
		SetGlobalTimer("gaze","LOCALS",6)
	RESPONSE #50
		Continue()
END

IF
	!GlobalTimerNotExpired("gaze","LOCALS")
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		ForceSpellRES("A^UMGAZE",LastSeenBy(Myself)) // Disorienting Gaze
		SetSequence(SEQ_READY)
		SetGlobalTimer("gaze","LOCALS",6)
END

IF
	!CheckStatGT(ThirdNearestEnemyOf(Myself),99,RESISTSLASHING)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!CheckStatGT(SecondNearestEnemyOf(Myself),99,RESISTSLASHING)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!CheckStatGT(NearestEnemyOf(Myself),99,RESISTSLASHING)
	See(NearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP6","LOCALS")
	!CheckStatGT(Player6,74,RESISTSLASHING)
	!StateCheck(Player6,STATE_PANIC)
	See(Player6)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP5","LOCALS")
	!CheckStatGT(Player5,74,RESISTSLASHING)
	!StateCheck(Player5,STATE_PANIC)
	See(Player5)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP4","LOCALS")
	!CheckStatGT(Player4,74,RESISTSLASHING)
	!StateCheck(Player4,STATE_PANIC)
	See(Player4)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP3","LOCALS")
	!CheckStatGT(Player3,74,RESISTSLASHING)
	!StateCheck(Player3,STATE_PANIC)
	See(Player3)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP2","LOCALS")
	!CheckStatGT(Player2,74,RESISTSLASHING)
	!StateCheck(Player2,STATE_PANIC)
	See(Player2)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP5","LOCALS")
	!CheckStatGT(Player1,74,RESISTSLASHING)
	!StateCheck(Player1,STATE_PANIC)
	See(Player1)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	Range(WorstAC,10)
	!GlobalTimerNotExpired("AVP5","LOCALS")
	!GlobalTimerNotExpired("AVP4","LOCALS")
	!GlobalTimerNotExpired("AVP3","LOCALS")
	!GlobalTimerNotExpired("AVP2","LOCALS")
	!GlobalTimerNotExpired("AVP1","LOCALS")
	!CheckStatGT(WorstAC,0,STONESKINS)
	!StateCheck(WorstAC,STATE_MIRRORIMAGE)
	!CheckStatGT(WorstAC,49,RESISTSLASHING)
	!StateCheck(WorstAC,STATE_PANIC)
	See(WorstAC)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	Range(MostDamagedOf(Myself),10)
	!GlobalTimerNotExpired("AVP5","LOCALS")
	!GlobalTimerNotExpired("AVP4","LOCALS")
	!GlobalTimerNotExpired("AVP3","LOCALS")
	!GlobalTimerNotExpired("AVP2","LOCALS")
	!GlobalTimerNotExpired("AVP1","LOCALS")
	!CheckStatGT(MostDamagedOf(Myself),0,STONESKINS)
	!StateCheck(MostDamagedOf(Myself),STATE_MIRRORIMAGE)
	!CheckStatGT(MostDamagedOf(Myself),49,RESISTSLASHING)
	!StateCheck(MostDamagedOf(Myself),STATE_PANIC)
	See(MostDamagedOf(Myself))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP6","LOCALS")
	!CheckStatGT(Player6,0,STONESKINS)
	!StateCheck(Player6,STATE_MIRRORIMAGE)
	!CheckStatGT(Player6,49,RESISTSLASHING)
	Range(Player6,10)
	OR(5)
		Class(Player6,LONG_BOW)
		Class(Player6,BARD)
		Class(Player6,DRUID)
		Class(Player6,CLERIC)
		HPPercentLT(Player6,50)
	!StateCheck(Player6,STATE_PANIC)
	See(Player6)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP5","LOCALS")
	!CheckStatGT(Player5,0,STONESKINS)
	!StateCheck(Player5,STATE_MIRRORIMAGE)
	!CheckStatGT(Player5,49,RESISTSLASHING)
	Range(Player5,10)
	OR(5)
		Class(Player5,LONG_BOW)
		Class(Player5,BARD)
		Class(Player5,DRUID)
		Class(Player5,CLERIC)
		HPPercentLT(Player5,50)
	!StateCheck(Player5,STATE_PANIC)
	See(Player5)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP4","LOCALS")
	!CheckStatGT(Player4,0,STONESKINS)
	!StateCheck(Player4,STATE_MIRRORIMAGE)
	!CheckStatGT(Player4,49,RESISTSLASHING)
	Range(Player4,10)
	OR(5)
		Class(Player4,LONG_BOW)
		Class(Player4,BARD)
		Class(Player4,DRUID)
		Class(Player4,CLERIC)
		HPPercentLT(Player4,50)
	!StateCheck(Player4,STATE_PANIC)
	See(Player4)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP3","LOCALS")
	!CheckStatGT(Player3,0,STONESKINS)
	!StateCheck(Player3,STATE_MIRRORIMAGE)
	!CheckStatGT(Player3,49,RESISTSLASHING)
	Range(Player3,10)
	OR(5)
		Class(Player3,LONG_BOW)
		Class(Player3,BARD)
		Class(Player3,DRUID)
		Class(Player3,CLERIC)
		HPPercentLT(Player3,50)
	!StateCheck(Player3,STATE_PANIC)
	See(Player3)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP2","LOCALS")
	!CheckStatGT(Player2,0,STONESKINS)
	!StateCheck(Player2,STATE_MIRRORIMAGE)
	!CheckStatGT(Player2,49,RESISTSLASHING)
	Range(Player2,10)
	OR(5)
		Class(Player2,LONG_BOW)
		Class(Player2,BARD)
		Class(Player2,DRUID)
		Class(Player2,CLERIC)
		HPPercentLT(Player2,50)
	!StateCheck(Player2,STATE_PANIC)
	See(Player2)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("AVP1","LOCALS")
	!CheckStatGT(Player1,0,STONESKINS)
	!StateCheck(Player1,STATE_MIRRORIMAGE)
	!CheckStatGT(Player1,49,RESISTSLASHING)
	Range(Player1,10)
	OR(5)
		Class(Player1,LONG_BOW)
		Class(Player1,BARD)
		Class(Player1,DRUID)
		Class(Player1,CLERIC)
		HPPercentLT(Player1,50)
	!StateCheck(Player1,STATE_PANIC)
	See(Player1)
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	Global("Target","Locals",1)
	Range(NearestEnemyOf(Myself),5)
	!InParty(NearestEnemyOf(Myself))
	!CheckStatGT(NearestEnemyOf(Myself),99,RESISTSLASHING)
	See(NearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	Global("Target","Locals",1)
	Range(NearestEnemyOf(Myself),5)
	InParty(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("AVP5","LOCALS")
	!GlobalTimerNotExpired("AVP4","LOCALS")
	!GlobalTimerNotExpired("AVP3","LOCALS")
	!GlobalTimerNotExpired("AVP2","LOCALS")
	!GlobalTimerNotExpired("AVP1","LOCALS")
	!CheckStatGT(NearestEnemyOf(Myself),99,RESISTSLASHING)
	See(NearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	Global("Target","Locals",1)
	Range(LastAttackerOf(Myself),5)
	InParty(LastAttackerOf(Myself))
	!GlobalTimerNotExpired("AVP5","LOCALS")
	!GlobalTimerNotExpired("AVP4","LOCALS")
	!GlobalTimerNotExpired("AVP3","LOCALS")
	!GlobalTimerNotExpired("AVP2","LOCALS")
	!GlobalTimerNotExpired("AVP1","LOCALS")
	!CheckStatGT(LastAttackerOf(Myself),99,RESISTSLASHING)
	See(LastAttackerOf(Myself))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	Global("Target","Locals",1)
	Range(LastAttackerOf(Myself),5)
	!InParty(LastAttackerOf(Myself))
	!CheckStatGT(LastAttackerOf(Myself),99,RESISTSLASHING)
	See(LastAttackerOf(Myself))
	False()
THEN
	RESPONSE #100
		Continue()
END

IF
	!InWeaponRange(LastSeenBy(Myself))	
	Exists(LastAttackerOf(LastSeenBy(Myself)))
	NumCreatureGT([ENEMY],1)
	Delay(6)
THEN
	RESPONSE #100
		RandomWalk()
END

IF
	See(LastSeenBy(Myself))
	!CheckStatGT(LastSeenBy(Myself),99,RESISTSLASHING)
	OR(2)
		!Allegiance(LastSeenBy(Myself),EVILCUTOFF)
		!Race(LastSeenBy(Myself),UMBERHULK)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate(LastSeenBy(Myself),15)
END

IF
	ActionListEmpty()
	See([GOODCUTOFF])
	Range([GOODCUTOFF],4)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	ActionListEmpty()
	Delay(4)
	See([GOODCUTOFF])
	!Range([GOODCUTOFF],4)
THEN
	RESPONSE #100
		MoveToObject([GOODCUTOFF])
END

IF
	True()
THEN
	RESPONSE #100
		NoAction()
END